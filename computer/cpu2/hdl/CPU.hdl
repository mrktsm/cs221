CHIP CPU {

    IN  inM[16],
        instruction[16],
        reset;

    OUT outM[16],
        writeM,
        addressM[15],
        pc[15];

    PARTS:
    // check if A or C instruction
    Not(in=instruction[15], out=isAinstruction);
    
    // Loader figures out which registers to load
    Loader(d1=instruction[5], d2=instruction[4], d3=instruction[3], 
           ctype=instruction[15], 
           loadA=loadAFromALU, loadD=loadD, loadM=writeM);
    
    // A loads on A-instruction or when dest bit is set
    Or(a=isAinstruction, b=loadAFromALU, out=loadA);
    
    // pick instruction value or ALU output for A register
    Mux16(a=aluOut, b=instruction, sel=isAinstruction, out=aRegisterIn);
    
    // A register
    ARegister(in=aRegisterIn, load=loadA, out=aRegisterOut, out[0..14]=addressM);
    
    // D register
    DRegister(in=aluOut, load=loadD, out=dRegisterOut);
    
    // select A or M for ALU
    Mux16(a=aRegisterOut, b=inM, sel=instruction[12], out=aOrM);
    
    // ALU does the computation
    ALU(x=dRegisterOut, y=aOrM, 
        zx=instruction[11], nx=instruction[10], 
        zy=instruction[9], ny=instruction[8], 
        f=instruction[7], no=instruction[6],
        out=aluOut, out=outM, zr=zr, ng=ng);
    
    // figure out if positive
    Or(a=zr, b=ng, out=notPositive);
    Not(in=notPositive, out=positive);
    
    // check jump conditions
    And(a=instruction[2], b=ng, out=jlt);
    And(a=instruction[1], b=zr, out=jeq);
    And(a=instruction[0], b=positive, out=jgt);
    
    // combine all jump checks
    Or(a=jlt, b=jeq, out=jle);
    Or(a=jle, b=jgt, out=jumpCondition);
    
    // only jump on C-instructions
    And(a=instruction[15], b=jumpCondition, out=jump);
    
    // program counter
    PC(in=aRegisterOut, load=jump, inc=true, reset=reset, out[0..14]=pc);
}
